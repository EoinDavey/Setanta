línte := []
nuair-a fíor {
    líne := léigh_líne()
    má !líne
        bris
    línte = línte + [líne]
}
>-- Aimsigh eochair i liosta --<
gníomh ams(ls, k){
    le i idir (0, fad(ls))
        má ls[i] == k
            toradh i
}

grph := []
le i idir (0, fad(línte))
    grph = grph + [roinn@(athchuir@(athchuir@(línte[i])(',', ''))('=> ', ''))(' ')]

>-- Eochracha --<
ecrcha := []
gníomh cuir_e(k){
    le i idir (0, fad(ecrcha))
        má ecrcha[i] == k
            bris
    ecrcha = ecrcha + [k]
}
cuir_e('ORE')
le i idir(0, fad(grph))
    cuir_e(grph[i][fad(grph[i])-1])
K := fad(ecrcha)

méad := [0]*K
adjLs := []
le i idir (0, K)
    adjLs = adjLs + [[]]

le i idir (0, fad(grph)){
    g := grph[i]
    ga := ams(ecrcha, g[fad(g)-1])
    gb := go_uimh(g[fad(g)-2])
    méad[ga] = gb
    le j idir(0, fad(g)) {
        má 2*j + 1 >= fad(g) - 2
            bris
        a := g[2*j]
        b := g[2*j+1]
        adjLs[ga] = adjLs[ga] + [[go_uimh(a), ams(ecrcha, b)]]
    }
}

feicthe := [breag]*K
ord := []
gníomh siul(u) {
    feicthe[u] = fíor
    ls := adjLs[u]
    le i idir (0, fad(ls)){
        v := ls[i][1]
        má !feicthe[v]
            siul(v)
    }
    ord = ord + [u]
}

siul(ams(ecrcha, 'FUEL'))

gníomh idiv(a, b) {
    toradh (a - (a % b))/b
}

gníomh reitigh(tgt) {
    reqs := [0]*K
    reqs[ams(ecrcha, 'FUEL')] = tgt
    ol := ams(ecrcha, 'ORE')
    le i idir (0, fad(ord)) {
        ind := fad(ord) - i - 1
        x := ord[ind]
        r := reqs[x]
        má x == ol 
            toradh r
        m := méad[x]
        tms := idiv(r+m-1, m)
        le j idir (0, fad(adjLs[x]))
            reqs[adjLs[x][j][1]] = reqs[adjLs[x][j][1]] + tms * adjLs[x][j][0]
    }
}

TR := 1000000000000
L := 0
R := TR

nuair-a L < R {
    md := idiv(L+R+1,2)
    v := reitigh(md)
    má v > TR
        R = md -1
    nó
        L = md
}

scríobh('Cuid A')
scríobh(reitigh(1))
scríobh('Cuid B')
scríobh(L)
