Program     := stmts=AsgnStmt* _
AsgnStmt    := IfStmt
             | BlockStmt
             | NuairStmt
             | LeStmt
             | CCStmt
             | BrisStmt
             | GniomhStmt
             | ToradhStmt
             | AssgnStmt
             | DefnStmt
             | Expr
NonAsgnStmt := IfStmt
             | NuairStmt
             | LeStmt
             | CCStmt
             | BrisStmt
             | ToradhStmt
             | BlockStmt
             | AssgnStmt
             | Expr
IfStmt      := _ 'm[áa]' &gap expr=Expr &gap stmt=NonAsgnStmt elsebranch={_ 'n[oó]' &gap stmt=NonAsgnStmt}?
BlockStmt   := _ '{' blk=AsgnStmt* _ '}'
NuairStmt   := _ 'nuair' gap 'a' expr=Expr &gap stmt=NonAsgnStmt
LeStmt      := _ 'le' &gap id=ID _ 'idir' _ '\('strt=Expr _ ',' end=Expr _ '\)' stmt=NonAsgnStmt
DefnStmt    := _ id=ID _ ':=' _ expr=Expr
AssgnStmt   := _ id=LSpec _ '=' _ expr=Expr
GniomhStmt  := _ 'gn[íi]omh' &gap id=ID _ '\(' args=CSIDs? _ '\)' _ '{'
    stmts=AsgnStmt*
_ '}'
BrisStmt    := _ 'bris'
CCStmt      := _ 'chun\s+cinn'
ToradhStmt  := _ 'toradh' &gap exp=Expr?
Expr        := And
And         := head=Or tail={_ '\&' trm=Or}*
Or          := head=Eq tail={_ '\|' trm=Eq}*
Eq          := head=Comp tail={_ op='[!=]=' trm=Comp}*
Comp        := head=Sum tail={_ op=Compare trm=Sum}*
Sum         := head=Product tail={_ op=PlusMinus trm=Product}*
Product     := head=Prefix tail={_ op=MulDiv trm=Prefix}*
Prefix      := op='-|!'? pf=Postfix
Postfix     := at=Atom ops=PostOp*
PostOp      := '\(' args=CSArgs? _ '\)' | '\[' expr=Expr '\]'
Atom        :=  _ '\(' trm=Expr '\)'
             | Litreacha
             | Int
             | ID
             | Bool
             | Neamhni
             | ListLit
LSpec       := id=ID ops=PostOp*
CSArgs      := head=Expr tail={_ ',' exp=Expr}*
CSIDs       := head=ID tail={_ ',' id=ID}*
ListLit     := _ '\[' els=CSArgs? _ '\]'
PlusMinus   := '\+|-'
MulDiv      := '\*|\/|%'
Compare     := '(<=)|(>=)|<|>'
Keyword     := 'm[áa]' | 'n[oó]' | 'nuair\s+a' | 'f[ií]or|breag'
    | 'gn[ií]omh' | 'chun\s+cinn' | 'neamhn[ií]' | 'toradh'
ID          := _ !{Keyword gap} id='[a-zA-Z_áéíóúÁÉÍÓÚ]+'
Bool        := _ bool='f[ií]or|breag'
Neamhni     := _ 'neamhn[ií]'
Int         := _ int='-?[0-9]+'
Litreacha   := '\'' val='([^\'\\]|(\\.))*' '\''
_           := '(?:\s|>--(?:(?!--<).)*(--<|\n))*'
gap         := '(^|\s|$|[^a-zA-Z0-9áéíóúÁÉÍÓÚ])+'
