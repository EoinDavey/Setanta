Program     := AsgnStmt*
AsgnStmt    := IfStmt
             | BlockStmt
             | NuairStmt
             | LeStmt
             | AssgnStmt
             | DefnStmt
             | Expr
NonAsgnStmt := IfStmt | NuairStmt | LeStmt | BlockStmt | AssgnStmt | Expr
IfStmt      := _ 'm[áa]' _ expr=Expr _ stmt=NonAsgnStmt elsebranch={'n[oó]' _  stmt=NonAsgnStmt}? _
BlockStmt   := _ '{' blk=AsgnStmt* '}' _
NuairStmt   := _ 'nuair a' expr=Expr stmt=NonAsgnStmt _
LeStmt      := _ 'le' _ id=ID _ 'idir' _ '\('strt=Expr ',' end=Expr'\)' stmt=NonAsgnStmt
DefnStmt    := _ id=ID _ ':=' _ expr=Expr _
AssgnStmt   := _ id=ID _ '=' _ expr=Expr _
Expr        := And
And         := head=Or tail={'\&' trm=Or}*
Or          := head=Eq tail={'\|' trm=Eq}*
Eq          := head=Comp tail={op='[!=]=' trm=Comp}*
Comp        := head=Sum tail={op=Compare trm=Sum}*
Sum         := head=Product tail={op=PlusMinus trm=Product}*
Product     := head=Atom tail={op=MulDiv trm=Atom}*
Atom        := Int
             | Bool
             | ID
             | _ '\(' trm=Expr '\)' _
PlusMinus   := '\+|-'
MulDiv      := '\*|\/|%'
Compare     := '(<=)|(>=)|<|>'
Keyword     := 'm[áa]' | 'n[oó]' | 'nuair a' | 'f[ií]or|breag'
ID          := _ !Keyword id='[a-zA-Z_áéíóúÁÉÍÓÚ]+' _
Bool        := _ bool='f[ií]or|breag' _
Int         := _ int='[0-9]+' _
_           := '\s*'
